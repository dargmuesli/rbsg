import org.gradle.internal.jvm.*

plugins {
    id 'checkstyle'
    id 'java'
    id 'org.openjfx.javafxplugin' version '0.0.7' apply false
}

group 'de.uniks.se1ss19teamb'
version '0.0.0'

if (Jvm.current().javaVersion.java11Compatible) {
    apply plugin: 'org.openjfx.javafxplugin'

    javafx {
        modules = ['javafx.base', 'javafx.controls', 'javafx.graphics', 'javafx.fxml']
    }
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    jcenter()
}

sourceSets {
    integration {
        java {
            srcDir 'src/test-integration/java'
        }
    }
}

configurations {
    checkstyleOverride
}

tasks.withType(Checkstyle) {
    checkstyleClasspath = project.configurations.checkstyleOverride
    maxWarnings = 0
}

dependencies {
    checkstyleOverride('com.puppycrawl.tools:checkstyle:8.21')

    implementation group: 'commons-net', name: 'commons-net',version:'3.6'
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.5'
    implementation group: 'commons-io', name: 'commons-io', version: '2.5'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.3.1'
    implementation group: 'com.jfoenix', name: 'jfoenix', version: '8.0.8'
    implementation group: 'de.jensd', name: 'fontawesomefx-fontawesome', version: '4.7.0-5'
    implementation group: 'de.jensd', name: 'fontawesomefx-emojione', version: '2.2.7-2'
    implementation group: 'de.jensd', name: 'fontawesomefx-icons525', version: '3.0.0-4'
    implementation group: 'de.jensd', name: 'fontawesomefx-materialdesignfont', version: '1.7.22-4'
    implementation group: 'de.jensd', name: 'fontawesomefx-materialicons', version: '2.2.0-5'
    implementation group: 'io.github.typhon0', name:'AnimateFX', version: '1.2.1'
    implementation group: 'javax.websocket', name: 'javax.websocket-client-api', version: '1.0'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.8'
    implementation group: 'org.fulib', name: 'fulibYaml', version: '1.0+'
    implementation group: 'org.glassfish.tyrus.bundles', name: 'tyrus-standalone-client', version: '1.15'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.2'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.2'

    runtime group: 'de.jensd', name: 'fontawesomefx-commons', version: '8.15'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.+'
    testImplementation group: 'org.fulib', name: 'fulib', version: '1.0+'

    integrationImplementation sourceSets.main.output
    integrationImplementation sourceSets.test.output
    integrationImplementation configurations.testImplementation
}

task testIntegration(type: Test) {
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
    debug = false
}