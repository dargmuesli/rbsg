import org.gradle.internal.jvm.*

plugins {
    id 'application'
    id 'checkstyle'
    id 'com.github.johnrengelman.shadow' version '5.0.0'
    id 'org.openjfx.javafxplugin' version '0.0.7' apply false
}

if (Jvm.current().javaVersion.java11Compatible) {
    apply plugin: 'org.openjfx.javafxplugin'

    javafx {
        modules = ['javafx.base', 'javafx.controls', 'javafx.graphics', 'javafx.fxml', 'javafx.media']
    }
}

group 'de.uniks.se1ss19teamb'
version '0.0.0'

repositories {
    jcenter()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    mavenCentral()
}

application {
    mainClassName = 'de.uniks.se1ss19teamb.rbsg.Main'
}

configurations {
    checkstyleOverride
}

sourceCompatibility = 1.8

sourceSets {
    integration {
        java {
            srcDir 'src/test-integration/java'
        }
    }
}

dependencies {
    checkstyleOverride group: 'com.puppycrawl.tools', name: 'checkstyle', version: '8.21'

    implementation group: 'com.google.code.gson', name: 'gson', version: '2.3.1'
    implementation group: 'com.jfoenix', name: 'jfoenix', version: '8.0.8'
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.5'
    implementation group: 'commons-io', name: 'commons-io', version: '2.5'
    implementation group: 'commons-net', name: 'commons-net', version: '3.6'
    implementation group: 'de.jensd', name: 'fontawesomefx-commons', version: '8.15'
    implementation group: 'de.jensd', name: 'fontawesomefx-emojione', version: '2.2.7-2'
    implementation group: 'de.jensd', name: 'fontawesomefx-fontawesome', version: '4.7.0-5'
    implementation group: 'de.jensd', name: 'fontawesomefx-icons525', version: '3.0.0-4'
    implementation group: 'de.jensd', name: 'fontawesomefx-materialdesignfont', version: '1.7.22-4'
    implementation group: 'de.jensd', name: 'fontawesomefx-materialicons', version: '2.2.0-5'
    implementation group: 'io.github.typhon0', name: 'AnimateFX', version: '1.2.1'
    implementation group: 'javax.websocket', name: 'javax.websocket-client-api', version: '1.0'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.8'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.2'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.2'
    implementation group: 'org.fulib', name: 'fulibYaml', version: '1.0+'
    implementation group: 'org.glassfish.tyrus.bundles', name: 'tyrus-standalone-client', version: '1.15'

    integrationImplementation configurations.testImplementation
    integrationImplementation sourceSets.main.output
    integrationImplementation sourceSets.test.output

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.fulib', name: 'fulib', version: '1.0+'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.+'
    testImplementation group: 'org.testfx', name: 'openjfx-monocle', version: '8u76-b04'
    testImplementation group: 'org.testfx', name: 'testfx-core', version: '4.0.15-alpha'
    testImplementation group: 'org.testfx', name: 'testfx-junit5', version: '4.0.15-alpha'
}

task testIntegration(type: Test) {
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
    debug = false
}

tasks.withType(Checkstyle) {
    checkstyleClasspath = project.configurations.checkstyleOverride
    maxWarnings = 0
}